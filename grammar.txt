program = {funcDecl}
funcDecl = "func" ident paramList ["out" paramList] block
paramList = "(" {ident ":" varType ","} [ident ":" varType]  ")"
block = "{" {statement} "}"
statement = funcCall
            | varDecl
            | varAssignment
            | ifStatement
            | forStatement
            | whileStatement
            | jumpStatement
ifStatement = "if" expr block
forStatement = "for" forClause block
forClause = [statement] ";" [expr] ";" [statement]
whileStatement = "while" expr block
jumpStatement = "continue" | "break" | "return"
varDecl = "let" ident ":" varType [assignOp expr]
varAssignment = varRef assignOp expr
varRef = ident {"[" expr "]"}
varType = ident {"[" [expr] "]"}
funcCall = ident argList
argList = "(" {expr ","} [expr] ")"


expr = logical
logical = equality {("&&" | "||") equality}
equality = comparison {("!=" | "==") comparison } ;
comparison = addition {(">" | ">=" | "<" | "<=") addition}
addition = multiplication {( "-" | "+" ) multiplication}
multiplication = unary {( "/" | "*" ) unary}
unary = (("!" | "-" | "+") unary) | primary
primary = funcCall | varRef | INT | FLOAT | STRING | "false" | "true" | "nil" | "(" expr ")"

// Missing... class, arrays of arrays, switch, varDeclList, newline/semicolon separators, some operators, ranges, lot of literals
